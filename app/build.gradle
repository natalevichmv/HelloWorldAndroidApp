apply plugin: 'com.android.application'

def key_alias = System.getenv("KEYALIAS")
def key_password = System.getenv("KEYPASS")
def store_file = System.getenv("STOREFILE")
def store_password = System.getenv("STOREPASS")

if (store_file == null) {
    def keystorePropertiesFile = file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    key_alias = keystoreProperties['keyAlias']
    key_password = keystoreProperties['keyPassword']
    store_file = keystoreProperties['storeFile']
    store_password = keystoreProperties['storePassword']
}

android {
    signingConfigs {
        config {
            keyAlias key_alias
            keyPassword key_password
            storeFile rootProject.file(store_file)
            storePassword store_password
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.helloworldapp"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
